plugins {
    id 'io.spring.dependency-management'
    id 'java'
    id 'idea'
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

def moduleName = "emmy.application"

version 'unspecified'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'org.junit.jupiter.api',
                '--add-reads', "$moduleName=org.junit.jupiter.api",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

sourceCompatibility = 11.0
targetCompatibility = 11.0

def junitVersion = '5.7.0'
def mockitoVersion = '3.5.0'

def bootstrapVersion = '4.5.0'
def jqueryVersion = '3.5.0'
def popperVersion = '1.16.0'

dependencies {
    implementation project(":emmy-common")
    implementation project(":emmy-domain")
    implementation project(":emmy-persistance")

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.sun.xml.messaging.saaj:saaj-impl:1.5.0'
    implementation group: 'javax.xml.soap', name: 'javax.xml.soap-api', version: '1.3.5'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.postgresql:postgresql:42.1.1'

    implementation 'org.springframework.boot:spring-boot-starter-security'

    testImplementation project(":emmy-persistance")

    // Tests dependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: "com.vaadin.external.google", module: "android-json"
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'junit', module: 'junit'
    }

    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: "${junitVersion}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitVersion}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.199'

    testImplementation "org.junit.platform:junit-platform-runner:1.5.2"
}

test {
    useJUnitPlatform()
}
