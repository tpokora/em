plugins {
    id 'io.spring.dependency-management'
    id 'java'
    id 'idea'
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

def moduleName = "emmy.application"

version 'unspecified'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'org.junit.jupiter.api',
                '--add-reads', "$moduleName=org.junit.jupiter.api",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}


dependencies {
    implementation project(":emmy-common")
    implementation project(":emmy-persistance")

    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.5.2"
    testImplementation "org.junit.platform:junit-platform-runner:1.5.2"
}

test {
    useJUnitPlatform()
}
